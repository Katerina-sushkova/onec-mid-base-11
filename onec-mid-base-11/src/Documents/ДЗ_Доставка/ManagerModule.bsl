 #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Анкета";
	КомандаПечати.Представление = НСтр("ru = 'Анкета о доставке'");
	КомандаПечати.Порядок = 5;
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 10;  
	
	//Комплкет документов
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Анкета,ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 75; 
	
	// Печать Договора
	
	КомандаПечати = КомандыПечати.Добавить();  
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.ДЗ_Доставка.ПФ_DOC_ДоговорПеревозкиГруза";
	КомандаПечати.Представление = НСтр("ru = 'Договор перевозки груза (конф)'");
	КомандаПечати.Порядок = 120; 

		
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Анкета");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета о доставке'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ДЗ_Доставка.ПФ_MXL_Анкета";
	КонецЕсли; 
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТранспортнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТранспортнаяНакладная(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'ТранспортнаяНакладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ДЗ_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли; 
		
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Анкета";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДЗ_Доставка.ПФ_MXL_Анкета");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиТабличныеЧасти(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТранспортнаяНакладная(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТранспортнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДЗ_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиРеквизитыКонтрагентовТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиТабличныеТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

     
Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДЗ_Доставка.Ссылка КАК Ссылка,
	               |	ДЗ_Доставка.Номер КАК Номер,
	               |	ДЗ_Доставка.Дата КАК Дата,
	               |	ДЗ_Доставка.Организация КАК Организация,
	               |	ДЗ_Доставка.Контрагент КАК Контрагент,
	               |	ДЗ_Доставка.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ДЗ_Доставка КАК ДЗ_Доставка
	               |ГДЕ
	               |	ДЗ_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции
 
Процедура ВывестиЗаголовокАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Шапка");
	
	ДанныеПечати = Новый Структура;
	
	ШаблонЗаголовка = "Анкета о доставке № %1 от %2";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	
	//QR  
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	КартинкаКода = Новый Картинка (ДанныеQRКода);
	ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаКода;
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;		
		
	//QR
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
КонецПроцедуры
Процедура ВывестиЗаголовокТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Шапка");
	
	ДанныеПечати = Новый Структура;
	
	ШаблонЗаголовка = "Транспортная накладная № %1 от %2";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	
	//QR  
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	КартинкаКода = Новый Картинка (ДанныеQRКода);
	ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаКода;
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;		
		
	//QR
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
КонецПроцедуры
Процедура ВывестиРеквизитыКонтрагентовТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПоставщик = Макет.ПолучитьОбласть("Грузоотправитель");
	ОбластьПокупатель = Макет.ПолучитьОбласть("Грузополучатель"); 
	ОбластьПустая = Макет.ПолучитьОбласть("ПустаяОбласть");
	ОбластьГруз = Макет.ПолучитьОбласть("Груз");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Покупатель", ДанныеДокументов.Контрагент);
		
	ОбластьПоставщик.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьПоставщик);
	
	ОбластьПокупатель.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьПокупатель); 
	ТабличныйДокумент.Вывести(ОбластьПустая);
	ТабличныйДокумент.Вывести(ОбластьГруз);	
    ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);	
	
КонецПроцедуры
Процедура ВывестиТабличныеТранспортнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	//ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПустая2 = Макет.ПолучитьОбласть("ПустаяОбласть2"); 
	ОбластьПодвал = Макет.ПолучитьОбласть("Подписи");
	ОбластьПустая3 = Макет.ПолучитьОбласть("ПустаяОбласть3");
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	
	//ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
		
	ТабличныйДокумент.Вывести(ОбластьПустая2);	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	ТабличныйДокумент.Вывести(ОбластьПустая3);
	ТабличныйДокумент.Вывести(ОбластьИтого);
	
КонецПроцедуры

Процедура ВывестиТабличныеЧасти(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьТЧ1 = Макет.ПолучитьОбласть("ТЧ1");
	ОбластьТЧ2 = Макет.ПолучитьОбласть("ТЧ2");
	ОбластьТЧ3 = Макет.ПолучитьОбласть("ТЧ3");
	ОбластьТЧ4 = Макет.ПолучитьОбласть("ТЧ4"); 
	ОбластьТЧ5 = Макет.ПолучитьОбласть("ТЧ5");
	ОбластьТЧ6 = Макет.ПолучитьОбласть("ТЧ6");
	
	
	ТабличныйДокумент.Вывести(ОбластьТЧ1);
	ТабличныйДокумент.Вывести(ОбластьТЧ2); 
	ТабличныйДокумент.Вывести(ОбластьТЧ3);
    ТабличныйДокумент.Вывести(ОбластьТЧ4);
	ТабличныйДокумент.Вывести(ОбластьТЧ5); 
	ТабличныйДокумент.Вывести(ОбластьТЧ6); 
		
КонецПроцедуры

   
	
#КонецОбласти

#КонецЕсли